#!/usr/bin/env zsh

# =============================================================================
# In order (always /etc/<file> before each):
#
# - $ZDOTDIR/.zshenv
# - $ZDOTDIR/.zprofile  (if login shell)
# > $ZDOTDIR/.zshrc     (if interactive)
# - $ZDOTDIR/.zpreztorc (zprezto)
# - $ZDOTDIR/.zlogin    (if login shell)
# - $ZDOTDIR/.zlogout   (if exiting a login shell)
# =============================================================================

# Set a good term for interactivity
export TERM=xterm-256color
[ -n "$TMUX" ] && export TERM=tmux-256color

# Some zsh options (most others I like are set by zprezto)
setopt CHASE_LINKS       # Resolves symlinks on cd and expanding
setopt APPEND_CREATE     # Do not yell when a file doesn't exist at append
setopt C_PRECEDENCES     # For arithmetics!

# Custom completions (before zprezto compiles them)
if type brew &>/dev/null; then
  fpath=("$(brew --prefix)/share/zsh/site-functions" $fpath)
  fpath=("$(brew --prefix)/share/zsh-completions" $fpath)
fi

# Load Prezto
source "$HOME/.zprezto/init.zsh" # This loads .zpreztorc

# Prompt: Show number of background jobs
PROMPT='%(1j.[%j] .)'$PROMPT

# Prompt: Show red exit code on right, with timestamp
precmd_pipestatus() {
  local exitcodes="${(j.|.)pipestatus}"
  RPROMPT='%F{white}[%*]'
  if [[ "$exitcodes" != "0" ]]; then
    RPROMPT="%F{$prompt_pure_colors[prompt:error]}[$exitcodes]%f $RPROMPT"
  fi
}
add-zsh-hook precmd precmd_pipestatus
